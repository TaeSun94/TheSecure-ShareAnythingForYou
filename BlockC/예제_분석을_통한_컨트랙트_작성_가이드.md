## **예제 분석을 통한 컨트랙트 작성 가이드**

#### **간단한 공개 경매**

```
//코드가 Solidity 0.4.21버전을 기반으로 작성
pragma solidity ^0.4.21;


contract SimpleAuction {
	//address: 20바이트의 크기를 담을수 있다. 모든 컨트랙트의 기반이 되는 변수
	//uint: 다양한 크기의 부호있는 정수 타입, 부호없는 정수타입이 존재
    address public beneficiary;
    uint public auctionEnd;

	//옥션의 현재 상황
    address public highestBidder;
    uint public highestBid;

	//이전 가격 제시들의 수락된 출금
    mapping(address => uint) pendingReturns;

    bool ended;

    event HighestBidIncreased(address bidder, uint amount);
    event AuctionEnded(address winner, uint amount);

	//유저가 트랜잭션에 대한 확인 요청을 받을 때 보여지는 메소드
    function SimpleAuction(
        uint _biddingTime,
        address _beneficiary
    ) public {
        beneficiary = _beneficiary;
        auctionEnd = now + _biddingTime;
    }

	//경매에 대한 가격제시와 같은 이 트랜잭션이 함께 보내진다.
	//경매에 낙찰되지 못했을 때만 반환 받을 수 있다.
    function bid() public payable {
	//payable키워드는 이더를 지급하는 것이 가능하도록 메소드에게 요구
	//require은 에러를 검출하고 추적하는 역할을 담당. try, catch문같은거라고 생각하면 될듯
		
		//경매 기간이 끝났으면 지금으로 돌아옴
        require(now <= auctionEnd);
        
        //만약 가격제시가 더 높지 않다면, 돈을 되돌려 보내줌
        require(msg.value > highestBid);

        if (highestBid != 0) {
        	//받는 사람이 그들의 돈을 스스로 출금하는 것이 안전하기 때문에 highestBidder.send(highestBid)대신 이를 사용
            pendingReturns[highestBidder] += highestBid;
        }
        highestBidder = msg.sender;
        highestBid = msg.value;
        emit HighestBidIncreased(msg.sender, msg.value);
    }

	//가장 비싼 값을 출금
    function withdraw() public returns (bool) {
        uint amount = pendingReturns[msg.sender];
        if (amount > 0) {
            pendingReturns[msg.sender] = 0;

            if (!msg.sender.send(amount)) {
                pendingReturns[msg.sender] = amount;
                return false;
            }
        }
        return true;
    }

	//경매를 끝내고 최고 가격을 수혜자에게 송금
    function auctionEnd() public {
		//1. 조건을 확인
		//2. 동작을 수행
		//3. 타 컨트랙트와 상호작용
		//위와 같은 방식으로 호출되는 것이 좋은 가이드 라인
		
		//1번 실행
        require(now >= auctionEnd);
        require(!ended); 

		//2번 실행
        ended = true;
        emit AuctionEnded(highestBidder, highestBid);
	
		//3번 실행
        beneficiary.transfer(highestBid);
    }
}
```

<hr>
#### **블라인드 경매**

위와 가장 큰 차이점은 기간마감에 대한 시간 압박이 없다는 것.

```
pragma solidity ^0.4.21;

contract BlindAuction {
    struct Bid {
        bytes32 blindedBid;
        uint deposit;
    }

    address public beneficiary;
    uint public biddingEnd;
    uint public revealEnd;
    bool public ended;

    mapping(address => Bid[]) public bids;

    address public highestBidder;
    uint public highestBid;

	//이전 가격 제시 출금
    mapping(address => uint) pendingReturns;

    event AuctionEnded(address winner, uint highestBid);

	//modifier는 함수 입력값을 입증하는 방법
    modifier onlyBefore(uint _time) { require(now < _time); _; }
    modifier onlyAfter(uint _time) { require(now > _time); _; }

    function BlindAuction(
        uint _biddingTime,
        uint _revealTime,
        address _beneficiary
    ) public {
        beneficiary = _beneficiary;
        biddingEnd = now + _biddingTime;
        revealEnd = biddingEnd + _revealTime;
    }

	//blindedBid는 keccak256(value, fake, secret)와 함께 가려진 가격을 제시함
	//가격 제시가 드러내는 단계에서 올바르게 보여진다면 보내지는 이더는 환급받을 수만 있음
	//fake를 어떻게 설정하냐느에 따라 진짜 가격제시를 숨길수 있음
	//이를 이용하는게 블라인드 경매의 핵심원리인거 같다
    function bid(bytes32 _blindedBid)
        public
        payable
        onlyBefore(biddingEnd)
    {
        bids[msg.sender].push(Bid({
            blindedBid: _blindedBid,
            deposit: msg.value
        }));
    }

	//가려진 가격을 제시한다.
	//가려진 유효하지 않은 가겍 제시들을 되돌려 받을 수 있다.
	//높은 가격 제시를 제외하고 모든 가격 제시도 돌려 받을수 있는 메소드
    function reveal(
        uint[] _values,
        bool[] _fake,
        bytes32[] _secret
    )
        public
        onlyAfter(biddingEnd)
        onlyBefore(revealEnd)
    {
        uint length = bids[msg.sender].length;
        require(_values.length == length);
        require(_fake.length == length);
        require(_secret.length == length);

        uint refund;
        for (uint i = 0; i < length; i++) {
            var bid = bids[msg.sender][i];
            var (value, fake, secret) =
                    (_values[i], _fake[i], _secret[i]);
            if (bid.blindedBid != keccak256(value, fake, secret)) {
            	//가격 제시는 실제로 드러나지 않는다.
                continue;
            }
            refund += bid.deposit;
            if (!fake && bid.deposit >= value) {
                if (placeBid(msg.sender, value))
                    refund -= value;
            }
            //똑같은 가격으로 제시할 경우 재 요구가 안되게 설정함
            bid.blindedBid = bytes32(0);
        }
        msg.sender.transfer(refund);
    }

	//internal: 이 메소드가 이 콘트랙트 안에서 스스로만 호출 될수 있다는 의미를 갖는 것. private라고 생각하면 될듯?
    function placeBid(address bidder, uint value) internal
            returns (bool success)
    {
        if (value <= highestBid) {
            return false;
        }
        if (highestBidder != 0) {
            pendingReturns[highestBidder] += highestBid;
        }
        highestBid = value;
        highestBidder = bidder;
        return true;
    }

	//과제시된 제시금을 출금
    function withdraw() public {
        uint amount = pendingReturns[msg.sender];
        if (amount > 0) {
        	//수혜자가 전송하기 전에 이 메소드를 다시 호출할수 있으므로 매우 중요하다.
            pendingReturns[msg.sender] = 0;

            msg.sender.transfer(amount);
        }
    }
	
	//경매를 끝내고 가장 높은 가 제시를 수혜자에게 송금
    function auctionEnd()
        public
        onlyAfter(revealEnd)
    {
        require(!ended);
        emit AuctionEnded(highestBidder, highestBid);
        ended = true;
        beneficiary.transfer(highestBid);
    }
}
```